#!/bin/zsh

#Static, antibody bundle < ~/.zsh_plugins.txt > ~/.zsh_plugins.sh, source ~/.zsh_plugins.sh

# shortcut to this dotfiles path is $DOTFILES
export DOTFILES="$HOME/rc"

# your default editor
export EDITOR='vim'

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/zsh/*.zsh)

# This is needed to load OMZ plugins
export ZSH=`antibody home`/https-COLON--SLASH--SLASH-github.com-SLASH-robbyrussell-SLASH-oh-my-zsh
export ZSH_CACHE_DIR=$ZSH/cache

# load antibody plugins
source ~/.zsh_plugins.sh

# load everything but the path and completion files
for file in ${config_files:#*/completion.zsh}; do
  source "$file"
done

autoload -Uz compinit
typeset -i updated_at=$(date +'%j' -r ~/.zcompdump 2>/dev/null || stat -f '%Sm' -t '%j' ~/.zcompdump 2>/dev/null)
if [ $(date +'%j') != $updated_at ]; then
  compinit -i
else
  compinit -C -i
fi

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}; do
  source "$file"
done

unset config_files updated_at

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
[ -f ~/.localrc ] && . ~/.localrc
